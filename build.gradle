buildscript {
    ext {
        kotlinVersion = "1.8.10"
        kotlinCoroutinesVersion = "1.6.4"
        gradleVersion = "8.0.1"
        jacocoVersion = "0.8.8"

        composeBomVersion = "2023.03.00"
        composeCompilerVersion = "1.4.2"
        composeVersion = "1.4.0"
        composeM3Version = "1.0.1"
        composeNavVersion = "2.5.3"
        activityVersion = "1.6.1"
        coilVersion = "2.3.0"

        okhttpVersion = "4.10.0" // only used for tests
    }
}

plugins {
    id "com.android.application" version '8.0.1' apply false
    id "com.android.library" version '8.0.1' apply false
    id "org.jetbrains.kotlin.android" version "1.8.10" apply false
    id "org.sonarqube" version "4.0.0.2929"
}

sonar {
    properties {
        property "sonar.organization", "trustedshops-public"
        property "sonar.projectKey", "trustedshops-public_etrusted-android-trustbadge-library"
        property "sonar.projectName", "etrusted-android-trustbadge-library"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

project("app") {
    sonar {
        skipProject = true
    }
}

project("library") {
    sonar {
        skipProject = false
        properties {
            // sonar requires relative path for sources and binaries
            property "sonar.sources", "src/main/java"
            property "sonar.binaries", "build/tmp/kotlin-classes/debug"
            // sonar requires absolute path for lint and jacoco reports!
            property "sonar.androidLint.reportPaths", "$rootDir/library/build/reports/lint-results-debug.xml"
            property "sonar.coverage.jacoco.xmlReportPaths", "$rootDir/library/build/mergedReportDir/jacocoTestReport/jacocoTestReport.xml"
        }
    }
}

tasks.getByName("sonar") {
    dependsOn(tasks.findByPath(":library:lintDebug"))
    dependsOn(tasks.findByPath(":app:lintDebug"))
}
